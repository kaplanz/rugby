//! Parser for `Debugger` commands.

WHITESPACE    = _{ " " | "\t" | NEWLINE }

Int           = @{ Sign? ~ Num }
  Sign        = ${ ("+" | "-") }
UInt          = @{ Num }
  Num         = @{ Bin | Oct | Hex | Dec }
    Bin       = ${ ^"0b" ~ ASCII_BIN_DIGIT+ }
    Oct       = ${ ^"0o" ~ ASCII_OCT_DIGIT+ }
    Hex       = ${ ^"0x" ~ ASCII_HEX_DIGIT+ }
    Dec       = ${         ASCII_DIGIT+ }

Input         = _{ SOI ~ Command? ~ EOI }
  Keyword     = _{ KBreak | KContinue | KDelete | KHelp | KInfo | KList |
                   KRead | KSkip | KStep | KWrite }
    KBreak    = _{ ^"break"    | ^"b" }
    KContinue = _{ ^"continue" | ^"c" }
    KDelete   = _{ ^"delete"   | ^"d" }
    KHelp     = _{ ^"help"     | ^"h" }
    KInfo     = _{ ^"info"     | ^"i" }
    KList     = _{ ^"list"     | ^"l" }
    KRead     = _{ ^"read"     | ^"r" }
    KSkip     = _{ ^"skip"            }
    KStep     = _{ ^"step"     | ^"s" }
    KWrite    = _{ ^"write"    | ^"w" }
  Command     = _{ Break | Continue | Delete | Help | Info | List | Read |
                   Skip | Step | Write }
    Break     =  { KBreak ~ UInt }
    Continue  =  { KContinue }
    Delete    =  { KDelete ~ UInt }
    Help      =  { KHelp ~ Keyword? }
    Info      =  { KInfo ~ SInfo? }
      SInfo   = _{ KBreak }
    List      =  { KList }
    Read      =  { KRead ~ UInt }
    Skip      =  { KSkip ~ UInt ~ UInt }
    Step      =  { KStep }
    Write     =  { KWrite ~ UInt ~ Int }
