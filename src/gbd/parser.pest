//! Parser for `Debugger` commands.

WHITESPACE    = _{ " " | "\t" | NEWLINE }

Int           = @{ Sign? ~ Num }
  Sign        = ${ ("+" | "-") }
UInt          = @{ Num }
  Num         = @{ Bin | Oct | Hex | Dec }
    Bin       = ${ ^"0b" ~ ASCII_BIN_DIGIT+ }
    Oct       = ${ ^"0o" ~ ASCII_OCT_DIGIT+ }
    Hex       = ${ ^"0x" ~ ASCII_HEX_DIGIT+ }
    Dec       = ${         ASCII_DIGIT+ }

Input         = _{ SOI ~ Command? ~ EOI }
  Keyword     = _{ KBreak | KContinue | KDelete | KFreq | KHelp | KInfo |
                   KList | KQuit | KRead | KSkip | KStep | KWrite }
    KBreak    = _{ ^"break"    | ^"b" }
    KContinue = _{ ^"continue" | ^"c" }
    KDelete   = _{ ^"delete"   | ^"d" }
    KFreq     = _{ ^"freq"            }
    KHelp     = _{ ^"help"     | ^"h" }
    KInfo     = _{ ^"info"     | ^"i" }
    KList     = _{ ^"list"     | ^"l" }
    KQuit     = _{ ^"quit"     | ^"q" }
    KRead     = _{ ^"read"     | ^"r" }
    KSkip     = _{ ^"skip"            }
    KStep     = _{ ^"step"     | ^"s" }
    KWrite    = _{ ^"write"    | ^"w" }
  Command     = _{ Break | Continue | Delete | Freq | Help | Info | List |
                   Quit | Read | Skip | Step | Write }
    Break     =  { KBreak ~ UInt }
    Continue  =  { KContinue }
    Delete    =  { KDelete ~ UInt }
    Freq      =  { KFreq ~ SFreq }
      SFreq   = _{ Dot | Insn | Mach }
        Dot   =  { ^"tstate" | ^"t" | ^"dot"  | ^"d" }
        Insn  =  { ^"instruction"   | ^"insn" | ^"i" }
        Mach  =  { ^"machine"       | ^"mach" | ^"m" }
    Help      =  { KHelp ~ Keyword? }
    Info      =  { KInfo ~ SInfo? }
      SInfo   = _{ KBreak }
    List      =  { KList }
    Quit      =  { KQuit }
    Read      =  { KRead ~ UInt }
    Skip      =  { KSkip ~ UInt ~ UInt }
    Step      =  { KStep }
    Write     =  { KWrite ~ UInt ~ Int }
