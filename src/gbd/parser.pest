//! Parser for `Debugger` commands.

WHITESPACE    = _{ " " | "\t" | NEWLINE }

Int           = @{ ("+" | "-")? ~ UInt }
  UInt        = @{ Bin | Oct | Hex | Dec }
    Bin       = @{ ^"0b" ~ ASCII_BIN_DIGIT+ }
    Oct       = @{ ^"0o" ~ ASCII_OCT_DIGIT+ }
    Hex       = @{ ^"0x" ~ ASCII_HEX_DIGIT+ }
    Dec       = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }

Input         = _{ SOI ~ Command? ~ EOI }
  Keyword     = _{ KBreak | KContinue | KHelp | KList | KRead | KSkip | KStep |
                   KWrite }
    KBreak    = _{ ^"break"    | ^"b" }
    KContinue = _{ ^"continue" | ^"c" }
    KHelp     = _{ ^"help"     | ^"h" }
    KList     = _{ ^"list"     | ^"l" }
    KRead     = _{ ^"read"     | ^"r" }
    KSkip     = _{ ^"skip"            }
    KStep     = _{ ^"step"     | ^"s" }
    KWrite    = _{ ^"write"    | ^"w" }
  Command     = _{ Break | Continue | Help | List | Read | Skip | Step | Write }
    Break     =  { KBreak ~ UInt }
    Continue  =  { KContinue }
    Help      =  { KHelp ~ Keyword? }
    List      =  { KList }
    Read      =  { KRead ~ UInt }
    Skip      =  { KSkip ~ UInt ~ UInt }
    Step      =  { KStep }
    Write     =  { KWrite ~ UInt ~ Int }
