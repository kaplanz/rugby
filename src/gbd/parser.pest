//! Parser for `Debugger` commands.

WHITESPACE   = _{ " " | "\t" | NEWLINE }

int            = @{ ("+" | "-")? ~ uint }
  uint         = @{ bin | oct | hex | dec }
    bin        = @{ ^"0b" ~ ASCII_BIN_DIGIT+ }
    oct        = @{ ^"0o" ~ ASCII_OCT_DIGIT+ }
    hex        = @{ ^"0x" ~ ASCII_HEX_DIGIT+ }
    dec        = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }

input        = _{ SOI ~ command? ~ EOI }
  command    = _{ break | continue | list | read | skip | step | write }
    break    =  { (^"break" | ^"b") ~ uint }
    continue =  { (^"continue" | ^"c") }
    list     =  { (^"list" | ^"l") }
    read     =  { (^"read" | ^"r") ~ uint }
    skip     =  { (^"skip") ~ uint ~ uint }
    step     =  { (^"step" | ^"s") }
    write    =  { (^"write" | ^"w") ~ uint ~ int }
