//! Parser for `Debugger` commands.

WHITESPACE    = _{ " " | "\t" | NEWLINE }

Int           = @{ Sign? ~ Num }
  Sign        = ${ ("+" | "-") }
UInt          = @{ Num }
  Num         = @{ Bin | Oct | Hex | Dec }
    Bin       = ${ ^"0b" ~ ASCII_BIN_DIGIT+ }
    Oct       = ${ ^"0o" ~ ASCII_OCT_DIGIT+ }
    Hex       = ${ ^"0x" ~ ASCII_HEX_DIGIT+ }
    Dec       = ${         ASCII_DIGIT+ }
RangeBounds   =  { RangeInc | Range | RangeFrom | RangeToInc | RangeTo |
                   RangeFull }
  Range       = ${ UInt ~ ".."  ~ UInt }
  RangeFrom   = ${ UInt ~ ".."         }
  RangeFull   = ${        ".."         }
  RangeInc    = ${ UInt ~ "..=" ~ UInt }
  RangeTo     = ${        ".."  ~ UInt }
  RangeToInc  = ${        "..=" ~ UInt }

Input         = _{ SOI ~ Command? ~ EOI }
  Keyword     = _{ KBreak | KContinue | KDelete | KFreq | KHelp | KInfo |
                   KJump | KLoad | KList | KQuit | KRead | KReset | KSkip |
                   KStore | KStep | KWrite }
    KBreak    = _{ ^"break"    | ^"b"  }
    KContinue = _{ ^"continue" | ^"c"  }
    KDelete   = _{ ^"delete"   | ^"d"  }
    KFreq     = _{ ^"freq"             }
    KHelp     = _{ ^"help"     | ^"h"  }
    KInfo     = _{ ^"info"     | ^"i"  }
    KJump     = _{ ^"jump"     | ^"j"  }
    KList     = _{ ^"list"     | ^"l"  }
    KLoad     = _{ ^"load"     | ^"lw" }
    KQuit     = _{ ^"quit"     | ^"q"  }
    KRead     = _{ ^"read"     | ^"r"  }
    KReset    = _{ ^"reset"            }
    KSkip     = _{ ^"skip"             }
    KStep     = _{ ^"step"     | ^"s"  }
    KStore    = _{ ^"store"    | ^"sw" }
    KWrite    = _{ ^"write"    | ^"w"  }
  Register    = _{ WRegister }
    WRegister = _{ AF | BC | DE | HL | SP | PC }
      AF      =  { ^"af" }
      BC      =  { ^"bc" }
      DE      =  { ^"de" }
      HL      =  { ^"hl" }
      SP      =  { ^"sp" }
      PC      =  { ^"pc" }
  Command     = _{ Break | Continue | Delete | Freq | Help | Info | Jump |
                   Load | List | Quit | Read | Reset | Skip | Store | Step |
                   Write }
    Break     =  { KBreak ~ UInt }
    Continue  =  { KContinue }
    Delete    =  { KDelete ~ UInt }
    Freq      =  { KFreq ~ SFreq }
      SFreq   = _{ Dot | Insn | Mach }
        Dot   =  { ^"tstate" | ^"t" | ^"dot"  | ^"d" }
        Insn  =  { ^"instruction"   | ^"insn" | ^"i" }
        Mach  =  { ^"machine"       | ^"mach" | ^"m" }
    Help      =  { KHelp ~ Keyword? }
    Info      =  { KInfo ~ SInfo? }
      SInfo   = _{ KBreak }
    Jump      =  { KJump ~ UInt }
    List      =  { KList }
    Load      =  { KLoad ~ Register }
    Quit      =  { KQuit }
    Read      =  { KRead ~ (RangeBounds | UInt) }
    Reset     =  { KReset }
    Skip      =  { KSkip ~ UInt ~ UInt }
    Step      =  { KStep }
    Store     =  { KStore ~ Register ~ Int }
    Write     =  { KWrite ~ (RangeBounds | UInt) ~ Int }
