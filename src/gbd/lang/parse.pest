//! Parser for `Debugger` commands.

WHITESPACE    = _{ " " | "\t" }
EOL           = _{ ";" | NEWLINE }

Identifier    = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")+ }
Int           = @{ Sign? ~ Num }
  Sign        = ${ ("+" | "-") }
UInt          = @{ Num }
  Num         = @{ Bin | Oct | Hex | Dec }
    Bin       = ${ ^"0b" ~ ASCII_BIN_DIGIT+ }
    Oct       = ${ ^"0o" ~ ASCII_OCT_DIGIT+ }
    Hex       = ${ ^"0x" ~ ASCII_HEX_DIGIT+ }
    Dec       = ${         ASCII_DIGIT+ }
RangeBounds   =  { RangeInc | Range | RangeFrom | RangeToInc | RangeTo |
                   RangeFull }
  Range       = ${ UInt ~ ".."  ~ UInt }
  RangeFrom   = ${ UInt ~ ".."         }
  RangeFull   = ${        ".."         }
  RangeInc    = ${ UInt ~ "..=" ~ UInt }
  RangeTo     = ${        ".."  ~ UInt }
  RangeToInc  = ${        "..=" ~ UInt }

Filter        = @{ Directive ~ ("," ~ Filter)? }
  Directive   = @{ (Module ~ "=")? ~ Level }
    Module    = @{ Identifier ~ ("::" ~ Module)? }
    Level     = @{ LTrace | LDebug | LInfo | LWarn | LError | LOff }
      LTrace  = _{ ^"trace" }
      LDebug  = _{ ^"debug" }
      LInfo   = _{ ^"info" }
      LWarn   = _{ ^"warn" }
      LError  = _{ ^"error" }
      LOff    = _{ ^"off" }

Program       = _{ SOI ~ Command? ~ (EOL ~ Command?)* ~ EOL? ~ EOI }
  Keyword     = _{ KBreak | KContinue | KDelete | KFreq | KHelp | KInfo |
                   KJump | KLoad | KLog | KList | KQuit | KRead | KReset |
                   KSkip | KStore | KStep | KWrite }
    KBreak    =  { ^"break" | ^"brea" | ^"bre" | ^"br" | ^"b" }
    KContinue =  { ^"continue" | ^"continu" | ^"contin" | ^"conti" | ^"cont" |
                   ^"con" | ^"co" | ^"c" }
    KDelete   =  { ^"delete" | ^"delet" | ^"dele" | ^"del" | ^"de" | ^"d" }
    KFreq     =  { ^"frequency" | ^"frequenc" | ^"frequen" | ^"freque" |
                   ^"frequ" | ^"freq" | ^"fre" | ^"fr" | ^"f" }
    KHelp     =  { ^"help" | ^"hel" | ^"he" | ^"h" }
    KInfo     =  { ^"info" | ^"inf" | ^"in" | ^"i" }
    KJump     =  { ^"jump" | ^"jum" | ^"ju" | ^"j" }
    KList     =  { ^"list" | ^"lis" | ^"li" | ^"l" }
    KLoad     =  { ^"load" | ^"loa" | ^"lw" }
    KLog      =  { ^"log" | ^"lo" }
    KQuit     =  { ^"quit" | ^"qui" | ^"qu" | ^"q" }
    KRead     =  { ^"read" | ^"rea" | ^"re" | ^"r" }
    KReset    =  { ^"reset" | ^"rese" | ^"res" }
    KSkip     =  { ^"skip" | ^"ski" | ^"sk" }
    KStep     =  { ^"step" | ^"ste" | ^"st" | ^"s" }
    KStore    =  { ^"store" | ^"stor" | ^"sto" | ^"sw" }
    KWrite    =  { ^"write" | ^"writ" | ^"wri" | ^"wr" | ^"w" }
  Register    = _{ WRegister }
    WRegister = _{ AF | BC | DE | HL | SP | PC }
      AF      =  { ^"af" }
      BC      =  { ^"bc" }
      DE      =  { ^"de" }
      HL      =  { ^"hl" }
      SP      =  { ^"sp" }
      PC      =  { ^"pc" }
  Command     = _{ Break | Continue | Delete | Freq | Help | Info | Jump |
                   Load | Log | List | Quit | Read | Reset | Skip | Store |
                   Step | Write }
    Break     =  { KBreak ~ UInt }
    Continue  =  { KContinue }
    Delete    =  { KDelete ~ UInt }
    Freq      =  { KFreq ~ SFreq }
      SFreq   = _{ Dot | Insn | Mach }
        Dot   =  { ^"tstate" | ^"t" | ^"dot"  | ^"d" }
        Insn  =  { ^"instruction"   | ^"insn" | ^"i" }
        Mach  =  { ^"machine"       | ^"mach" | ^"m" }
    Help      =  { KHelp ~ Keyword? }
    Info      =  { KInfo ~ SInfo? }
      SInfo   = _{ KBreak }
    Jump      =  { KJump ~ UInt }
    List      =  { KList }
    Load      =  { KLoad ~ Register }
    Log       =  { KLog ~ Filter? }
    Quit      =  { KQuit }
    Read      =  { KRead ~ (RangeBounds | UInt) }
    Reset     =  { KReset }
    Skip      =  { KSkip ~ UInt ~ UInt }
    Step      =  { KStep }
    Store     =  { KStore ~ Register ~ Int }
    Write     =  { KWrite ~ (RangeBounds | UInt) ~ Int }
