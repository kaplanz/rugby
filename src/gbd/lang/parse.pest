//! Parser for `Debugger` commands.

WHITESPACE    = _{ " " | "\t" }
EOL           = _{ ";" | NEWLINE }

Identifier    = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")+ }
Int           = @{ Sign? ~ Num }
  Sign        = ${ ("+" | "-") }
UInt          = @{ Num }
  Num         = @{ Bin | Oct | Hex | Dec }
    Bin       = ${ ^"0b" ~ ASCII_BIN_DIGIT+ }
    Oct       = ${ ^"0o" ~ ASCII_OCT_DIGIT+ }
    Hex       = ${ ^"0x" ~ ASCII_HEX_DIGIT+ }
    Dec       = ${         ASCII_DIGIT+ }
RangeBounds   =  { RangeInc | Range | RangeFrom | RangeToInc | RangeTo |
                   RangeFull }
  Range       = ${ UInt ~ ".."  ~ UInt }
  RangeFrom   = ${ UInt ~ ".."         }
  RangeFull   = ${        ".."         }
  RangeInc    = ${ UInt ~ "..=" ~ UInt }
  RangeTo     = ${        ".."  ~ UInt }
  RangeToInc  = ${        "..=" ~ UInt }

Filter        = @{ Directive ~ ("," ~ Filter)? }
  Directive   = @{ (Module ~ "=")? ~ Level }
    Module    = @{ Identifier ~ ("::" ~ Module)? }
    Level     = @{ LTrace | LDebug | LInfo | LWarn | LError | LOff }
      LTrace  = _{ ^"trace" }
      LDebug  = _{ ^"debug" }
      LInfo   = _{ ^"info" }
      LWarn   = _{ ^"warn" }
      LError  = _{ ^"error" }
      LOff    = _{ ^"off" }

Program       = _{ SOI ~ Command? ~ (EOL ~ Command?)* ~ EOL? ~ EOI }
  Keyword     = _{ KBreak | KContinue | KDelete | KDisable | KEnable | KFreq |
                   KHelp | KIgnore | KInfo | KJump | KLoad | KLog | KList |
                   KQuit | KRead | KReset | KStore | KStep | KWrite }
    KBreak    =  { ^"break" | ^"brea" | ^"bre" | ^"br" | ^"b" }
    KContinue =  { ^"continue" | ^"continu" | ^"contin" | ^"conti" | ^"cont" |
                   ^"con" | ^"co" | ^"c" }
    KDelete   =  { ^"delete" | ^"delet" | ^"dele" | ^"del" | ^"de" }
    KDisable  =  { ^"disable" | ^"disabl" | ^"disab" | ^"disa" | ^"dis" | ^"di"
                    | ^"d"}
    KEnable   =  { ^"enable" | ^"enabl" | ^"enab" | ^"ena" | ^"en" | ^"e" }
    KFreq     =  { ^"frequency" | ^"frequenc" | ^"frequen" | ^"freque" |
                   ^"frequ" | ^"freq" | ^"fre" | ^"fr" | ^"f" }
    KHelp     =  { ^"help" | ^"hel" | ^"he" | ^"h" }
    KIgnore   =  { ^"ignore" | ^"ignor" | ^"igno" | ^"ign" | ^"ig" }
    KInfo     =  { ^"info" | ^"inf" | ^"in" | ^"i" }
    KJump     =  { ^"jump" | ^"jum" | ^"ju" | ^"j" }
    KList     =  { ^"list" | ^"lis" | ^"li" | ^"l" }
    KLoad     =  { ^"load" | ^"loa" | ^"ld" }
    KLog      =  { ^"log" | ^"lo" }
    KQuit     =  { ^"quit" | ^"qui" | ^"qu" | ^"q" }
    KRead     =  { ^"read" | ^"rea" | ^"re" | ^"r" }
    KReset    =  { ^"reset" | ^"rese" | ^"res" }
    KStep     =  { ^"step" | ^"ste"         | ^"s" }
    KStore    =  { ^"store" | ^"stor" | ^"sto" | ^"st" }
    KWrite    =  { ^"write" | ^"writ" | ^"wri" | ^"wr" | ^"w" }
  Location    = _{ (^"w"? ~ Loc16) | (^"b"? ~ Loc8) }
    Loc8      = _{ Ppu | Timer | Byte }
      Byte    =  { A | F | B | C | D | E | H | L }
        A     =  { ^"a" }
        F     =  { ^"f" }
        B     =  { ^"b" }
        C     =  { ^"c" }
        D     =  { ^"d" }
        E     =  { ^"e" }
        H     =  { ^"h" }
        L     =  { ^"l" }
      Ppu     =  { Lcdc | Stat | Scy | Scx | Lyc | Ly | Dma | Bgp | Obp0 |
                   Obp1 | Wy | Wx }
        Lcdc  =  { ^"lcdc" }
        Stat  =  { ^"stat" }
        Scy   =  { ^"scy" }
        Scx   =  { ^"scx" }
        Ly    =  { ^"ly" }
        Lyc   =  { ^"lyc" }
        Dma   =  { ^"dma" }
        Bgp   =  { ^"bgp" }
        Obp0  =  { ^"obp0" }
        Obp1  =  { ^"obp1" }
        Wy    =  { ^"wy" }
        Wx    =  { ^"wx" }
      Timer   =  { Div | Tima | Tma | Tac }
        Div   =  { ^"div" }
        Tima  =  { ^"tima" }
        Tma   =  { ^"tma" }
        Tac   =  { ^"tac" }
    Loc16     = _{ Word }
      Word    =  { AF | BC | DE | HL | SP | PC }
        AF    =  { ^"af" }
        BC    =  { ^"bc" }
        DE    =  { ^"de" }
        HL    =  { ^"hl" }
        SP    =  { ^"sp" }
        PC    =  { ^"pc" }
  Command     = _{ Break | Continue | Delete | Disable | Enable | Freq | Help |
                   Ignore | Info | Jump | Load | Log | List | Quit | Read |
                   Reset | Store | Step | Write }
    Break     =  { KBreak ~ UInt }
    Continue  =  { KContinue }
    Delete    =  { KDelete ~ UInt }
    Disable   =  { KDisable ~ UInt }
    Enable    =  { KEnable ~ UInt }
    Freq      =  { KFreq ~ SFreq }
      SFreq   = _{ Dot | Mach | Insn }
        Dot   =  { ^"tstate" | ^"t" | ^"dot"  | ^"d" }
        Insn  =  { ^"instruction"   | ^"insn" | ^"i" }
        Mach  =  { ^"machine"       | ^"mach" | ^"m" }
    Help      =  { KHelp ~ Keyword? }
    Ignore    =  { KIgnore ~ UInt ~ UInt }
    Info      =  { KInfo ~ SInfo? }
      SInfo   = _{ KBreak }
    Jump      =  { KJump ~ UInt }
    List      =  { KList }
    Load      =  { KLoad ~ Location }
    Log       =  { KLog ~ Filter? }
    Quit      =  { KQuit }
    Read      =  { KRead ~ (RangeBounds | UInt) }
    Reset     =  { KReset }
    Step      =  { KStep ~ UInt? }
    Store     =  { KStore ~ Location ~ Int }
    Write     =  { KWrite ~ (RangeBounds | UInt) ~ Int }
