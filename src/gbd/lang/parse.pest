//! Parser for `Debugger` commands.

WHITESPACE      = _{ " " | "\t" }
EOL             = _{ ";" | NEWLINE }

Identifier      = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")+ }
Int             = @{ Sign? ~ Num }
  Sign          = ${ ("+" | "-") }
UInt            = @{ Num }
  Num           = @{ Bin | Oct | Hex | Dec }
    Bin         = ${ ^"0b" ~ ASCII_BIN_DIGIT+ }
    Oct         = ${ ^"0o" ~ ASCII_OCT_DIGIT+ }
    Hex         = ${ ^"0x" ~ ASCII_HEX_DIGIT+ }
    Dec         = ${         ASCII_DIGIT+ }
RangeBounds     =  { RangeInc | Range | RangeFrom | RangeToInc | RangeTo |
                     RangeFull }
  Range         = ${ UInt ~ ".."  ~ UInt }
  RangeFrom     = ${ UInt ~ ".."         }
  RangeFull     = ${        ".."         }
  RangeInc      = ${ UInt ~ "..=" ~ UInt }
  RangeTo       = ${        ".."  ~ UInt }
  RangeToInc    = ${        "..=" ~ UInt }

Filter          = @{ Directive ~ ("," ~ Filter)? }
  Directive     = @{ (Module ~ "=" ~ Level) | Module | Level }
    Module      = @{ Identifier ~ ("::" ~ Module)? }
    Level       = @{ LTrace | LDebug | LInfo | LWarn | LError | LOff }
      LTrace    = _{ ^"trace" }
      LDebug    = _{ ^"debug" }
      LInfo     = _{ ^"info" }
      LWarn     = _{ ^"warn" }
      LError    = _{ ^"error" }
      LOff      = _{ ^"off" }

Program         = _{ SOI ~ Command? ~ (EOL ~ Command?)* ~ EOL? ~ EOI }
  Keyword       = _{ KBreak | KContinue | KDelete | KDisable | KEnable | KFreq |
                     KGoto | KHelp | KIgnore | KInfo | KJump | KLoad | KLog |
                     KList | KQuit | KReset | KRead | KSerial | KStore | KStep |
                     KWrite }
    KBreak      =  { ^"break"     | ^"br"   | ^"b" }
    KContinue   =  { ^"continue"  | ^"cont" | ^"c" }
    KDelete     =  { ^"delete"    | ^"del"         }
    KDisable    =  { ^"disable"   | ^"dis"  | ^"d" }
    KEnable     =  { ^"enable"    | ^"en"   | ^"e" }
    KFreq       =  { ^"frequency" | ^"freq" | ^"f" }
    KGoto       =  { ^"goto"      | ^"go"   | ^"g" }
    KHelp       =  { ^"help"                | ^"h" }
    KIgnore     =  { ^"ignore"    | ^"ig"          }
    KInfo       =  { ^"info"                | ^"i" }
    KJump       =  { ^"jump"      | ^"jp"   | ^"j" }
    KList       =  { ^"list"      | ^"ls"   | ^"l" }
    KLoad       =  { ^"load"      | ^"ld"          }
      KLoadB    =  { ^"lb" }
      KLoadW    =  { ^"lw" }
    KLog        =  { ^"log"       | ^"lo"          }
    KQuit       =  { ^"quit"                | ^"q" }
    KRead       =  { ^"read"      | ^"rd"   | ^"r" }
    KReset      =  { ^"reset"     | ^"res"         }
    KSerial     =  { ^"serial"    | ^"sx"          }
    KStep       =  { ^"step"                | ^"s" }
    KStore      =  { ^"store"     | ^"sr"          }
      KStoreB   =  { ^"sb" }
      KStoreW   =  { ^"sw" }
    KWrite      =  { ^"write"     | ^"wr"   | ^"w" }
  Location      = _{ WLocation | BLocation }
    BLocation   = _{ Pic | Ppu | SerialX | Timer | Byte }
      Byte      =  { A | F | B | C | D | E | H | L }
        A       =  { ^"a" }
        F       =  { ^"f" }
        B       =  { ^"b" }
        C       =  { ^"c" }
        D       =  { ^"d" }
        E       =  { ^"e" }
        H       =  { ^"h" }
        L       =  { ^"l" }
      Pic       =  { If | Ie }
        If      =  { ^"if" }
        Ie      =  { ^"ie" }
      Ppu       =  { Lcdc | Stat | Scy | Scx | Lyc | Ly | Dma | Bgp | Obp0 |
                     Obp1 | Wy | Wx }
        Lcdc    =  { ^"lcdc" }
        Stat    =  { ^"stat" }
        Scy     =  { ^"scy" }
        Scx     =  { ^"scx" }
        Ly      =  { ^"ly" }
        Lyc     =  { ^"lyc" }
        Dma     =  { ^"dma" }
        Bgp     =  { ^"bgp" }
        Obp0    =  { ^"obp0" }
        Obp1    =  { ^"obp1" }
        Wy      =  { ^"wy" }
        Wx      =  { ^"wx" }
      SerialX   =  { Sb | Sc }
        Sb      =  { ^"sb" }
        Sc      =  { ^"sc" }
      Timer     =  { Div | Tima | Tma | Tac }
        Div     =  { ^"div" }
        Tima    =  { ^"tima" }
        Tma     =  { ^"tma" }
        Tac     =  { ^"tac" }
    WLocation   = _{ Word }
      Word      =  { AF | BC | DE | HL | SP | PC }
        AF      =  { ^"af" }
        BC      =  { ^"bc" }
        DE      =  { ^"de" }
        HL      =  { ^"hl" }
        SP      =  { ^"sp" }
        PC      =  { ^"pc" }
  Command       = _{ Break | Continue | Delete | Disable | Enable | Freq |
                     Goto | Help | Ignore | Info | Jump | Load | Log | List |
                     Quit | Reset | Read | Serial | Store | Step | Write }
    Break       =  { KBreak ~ UInt }
    Continue    =  { KContinue }
    Delete      =  { KDelete ~ UInt }
    Disable     =  { KDisable ~ UInt }
    Enable      =  { KEnable ~ UInt }
    Freq        =  { KFreq ~ SFreq? }
      SFreq     = _{ Dot | Mach | Insn | Line | Frame }
        Dot     =  { ^"dot"                     | ^"d" }
        Mach    =  { ^"machine"       | ^"mach" | ^"m" }
        Insn    =  { ^"instruction"   | ^"insn" | ^"i" }
        Line    =  { ^"scanline"      | ^"line" | ^"l" }
        Frame   =  { ^"frame"                   | ^"f" }
    Goto        =  { KGoto ~ UInt }
    Help        =  { KHelp ~ Keyword? }
    Ignore      =  { KIgnore ~ UInt ~ UInt }
    Info        =  { KInfo ~ SInfo? }
      SInfo     = _{ KBreak }
    Jump        =  { KJump ~ UInt }
    List        =  { KList }
    Load        =  { LoadB | LoadW | (KLoad ~ Location+) }
      LoadB     = _{ KLoadB ~ BLocation+ }
      LoadW     = _{ KLoadW ~ WLocation+ }
    Log         =  { KLog ~ Filter? }
    Quit        =  { KQuit }
    Read        =  { KRead ~ (RangeBounds | UInt) }
    Reset       =  { KReset }
    Serial      =  { KSerial }
    Step        =  { KStep ~ UInt? }
    Store       =  { StoreB | StoreW | (KStore ~ Location+ ~ Int) }
      StoreB    = _{ KStoreB ~ BLocation+ ~ Int }
      StoreW    = _{ KStoreW ~ WLocation+ ~ Int }
    Write       =  { KWrite ~ (RangeBounds | UInt) ~ Int }
